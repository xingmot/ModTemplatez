apply from: 'gradle/scripts/helpers.gradle'

public void fnGenerateMixinJson() {
    def missingConfig = propertyStringList('mixin_configs').findAll(config -> !file("src/main/resources/${config}.json").exists())
    if (!propertyBool('use_mixin') || !propertyBool('generate_mixins_json') || missingConfig.empty) {
        return
    }
    def lines = """{
    "package": "",
    "required": true,
    "target": "@env(DEFAULT)",
    "minVersion": "0.8",
    "compatibilityLevel": "JAVA_8",
    "mixins": [],
    "server": [],
    "client": [],
    "injectors": {
        "defaultRequire": 1
    }
}
"""
    for (String mixinConfig : missingConfig) {
        file("src/main/resources/${mixinConfig}.json") << lines
    }
}

tasks.register('generateMixinJson') {
    group = 'custom helpers'
    doLast {
        fnGenerateMixinJson()
    }
}

public void fnGenerateAccessWidenerBase() {
    File f = file("src/main/resources/${propertyString('access_widener_name')}.accesswidener")
    if (!propertyBool('use_access_widener') || !propertyBool('generate_access_widener') || f.exists()) {
        return
    }
    def lines = """accessWidener v2 named
# Access Widener file generated by ModTemplatez
# If `use_access_widener` is not `true` or `access_widener_name` is not the file name of this file,
# this Access Widener file will not be used and can be deleted, see `gradle.properties` for more info

"""
    f << lines
}

tasks.register('generateAccessWidenerBase') {
    group = 'custom helpers'
    doLast {
        fnGenerateAccessWidenerBase()
    }
}

ext.fnGenerateAccessWidenerBase = this.&fnGenerateAccessWidenerBase
ext.fnGenerateMixinJson = this.&fnGenerateMixinJson
